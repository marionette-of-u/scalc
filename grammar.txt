<token_desc> token{
  <right>{
    unary_minus, unary_plus;
    double_slash<analyzer::binary_operator*>;
    hat<analyzer::binary_operator*>;
  }
  <left>{
    asterisk<analyzer::binary_operator*>, slash<analyzer::binary_operator*>;
    plus<analyzer::binary_operator*>, minus<analyzer::binary_operator*>;
  }
  left_paren, right_paren;
  <left>{
    right_arrow;
    equal;
    comma;
  }
  identifier<analyzer::value*>, symbol<analyzer::symbol*>, keyword_where, keyword_let;
}

<grammar> parser{
  Statement<analyzer::eval_target*>
    : [make_statement] BaseExpr(0) WhereEquality(1)
    | [define_symbol] keyword_let symbol(0) equal BaseExpr(1)
    ;

  WhereEquality<analyzer::equality_sequence*>
    : [identity] keyword_where EqualitySequence(0)
    | []
    ;

  EqualitySequence<analyzer::equality_sequence*>
    : [make_equality_sequence] Equality(0)
    | [make_equality_sequence] EqualitySequence(0) comma Equality(1)
    ;

  Equality<analyzer::equality*>
    : [make_equality] symbol(0) equal BaseExpr(1)
    ;

  BaseExpr<analyzer::eval_target*>
    : [make_add] BaseExpr(0) plus BaseExpr(1)
    | [make_sub] BaseExpr(0) minus BaseExpr(1)
    | [make_mul] BaseExpr(0) asterisk BaseExpr(1)
    | [make_div] BaseExpr(0) slash BaseExpr(1)
    | [make_pow] BaseExpr(0) hat BaseExpr(1)
    | [make_div] BaseExpr(0) double_slash BaseExpr(1)
    | [make_negate_expr] <unary_minus> minus BaseExpr(0)
    | [identity] <unary_plus> plus BaseExpr(0)
    | [identity] left_paren BaseExpr(0) right_paren
    | [identity] identifier(0)
    | [identity] symbol(0)
    ;
}
